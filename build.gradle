plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.21'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
}

group = 'dev.sora.relay'
version = '1.1.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://repo.opencollab.dev/maven-releases/' }
    maven { url = 'https://repo.opencollab.dev/maven-snapshots/' }
    maven { url = 'https://jitpack.io' }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    embed('com.nukkitx.protocol:bedrock-v560:2.9.15-SNAPSHOT') {
        exclude group: 'com.nukkitx.network'
        exclude group: 'com.nukkitx', module: 'natives'
        exclude group: 'org.lanternpowered'
    }
    embed project(':raknet')
    embed project(':natives')

    embed 'com.google.code.gson:gson:2.10'
    embed 'io.netty:netty-transport:4.1.85.Final'
    embed 'com.google.code.findbugs:jsr305:3.0.2'
    embed 'net.jodah:expiringmap:0.5.10'
    embed 'com.github.mccheatz:minecraft-msftauth:7dcece934a'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.embed]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}